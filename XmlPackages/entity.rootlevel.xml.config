<?xml version="1.0" standalone="yes" ?>
<package version="2.1" displayname="Entity Root Level Grid" debug="false" includeentityhelper="true">

	<!-- ###################################################################################################### -->
	<!-- Copyright AspDotNetStorefront.com, 1995-2007.  All Rights Reserved.					                -->
	<!-- http://www.aspdotnetstorefront.com														                -->
	<!-- For details on this license please visit  the product homepage at the URL above.		                -->
	<!-- THE ABOVE NOTICE MUST REMAIN INTACT.                                                                   -->
	<!-- $Header: /v6.1/Web/XmlPackages/entity.rootlevel.xml.config 1     12/30/05 2:33p Administrator $	    -->
	<!--                                                                                                        -->
	<!-- Notes: expects param of Entity to be passed in by querystring                                          -->
	<!-- e-entity.rootlevel.aspx?entity=Category                                                                -->
    <!-- the querystring param name must be all lower case and the values must be one of the following values   -->
    <!-- (case sensitive: Category, Section, Manufacturer, Library)                                             -->
	<!-- ###################################################################################################### -->

    <PackageTransform>
    
        <xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:aspdnsf="urn:aspdnsf" exclude-result-prefixes="aspdnsf">
          <xsl:output method="html" omit-xml-declaration="yes" />

            <xsl:param name="EntityName"><xsl:value-of select="/root/QueryString/entity" /></xsl:param>
            
            <xsl:param name="ShowInGrid">true</xsl:param>
            <xsl:param name="GridCols">4</xsl:param>

            <xsl:param name="LocaleSetting" select="/root/Runtime/LocaleSetting" />
            <xsl:param name="WebConfigLocaleSetting" select="/root/Runtime/WebConfigLocaleSetting" />

            <xsl:template match="/">
                <xsl:choose>
	               	<xsl:when test="$ShowInGrid = 'true'">
    	            <table border="0" cellpadding="0" cellspacing="4" width="100%">
						<xsl:apply-templates select="/root/EntityHelpers/*[name()=/root/QueryString/entity]/child::Entity" />
            	    </table>
					</xsl:when>
                	<xsl:otherwise>
						<xsl:apply-templates select="/root/EntityHelpers/*[name()=/root/QueryString/entity]/child::Entity" />
                	</xsl:otherwise>
                </xsl:choose>
            </xsl:template>

            <xsl:template match="Entity">
                <xsl:param name="scName">
                    <xsl:choose>
                        <xsl:when test="count(Name/ml/locale[@name=$LocaleSetting])!=0">
                            <xsl:value-of select="Name/ml/locale[@name=$LocaleSetting]"/>
                        </xsl:when>
                        <xsl:when test="count(Name/ml/locale[@name=$WebConfigLocaleSetting]) !=0 ">
                            <xsl:value-of select="Name/ml/locale[@name=$WebConfigLocaleSetting]"/>
                        </xsl:when>
                        <xsl:when test="count(Name/ml)=0">
                            <xsl:value-of select="Name"/>
                        </xsl:when>
                    </xsl:choose>
                </xsl:param>

                <xsl:choose>
                	<xsl:when test="$ShowInGrid = 'true'">
                            <xsl:if test="position() mod $GridCols = 1">
                                <tr>
                                 <xsl:for-each select=". | following-sibling::*[position() &lt; $GridCols]">
                                    <xsl:call-template name="EntityCell">
                                    </xsl:call-template>
                                 </xsl:for-each>
                                </tr>
                                <tr>
                                    <td height="10" colspan="{$GridCols}">&#0160;</td>
                                </tr>
                            </xsl:if>
                    </xsl:when>
                	<xsl:otherwise>
                        <p align="left">
                            &#0160;&#0160;&#0160;<img border="0" src="{concat('skins/skin_', aspdnsf:SkinID(), '/images/redarrow.gif')}"></img>&#0160;
                            <a href="{aspdnsf:EntityLink(EntityID, SEName, $EntityName, 0, '')}">
                                <xsl:value-of select="$scName" disable-output-escaping="yes"/>
                            </a>
                        </p>
                    </xsl:otherwise>
                </xsl:choose>
            </xsl:template>

            <xsl:template name="EntityCell">
                <xsl:param name="scName">
                    <xsl:choose>
                        <xsl:when test="count(Name/ml/locale[@name=$LocaleSetting])!=0">
                            <xsl:value-of select="Name/ml/locale[@name=$LocaleSetting]"/>
                        </xsl:when>
                        <xsl:when test="count(Name/ml/locale[@name=$WebConfigLocaleSetting]) !=0 ">
                            <xsl:value-of select="Name/ml/locale[@name=$WebConfigLocaleSetting]"/>
                        </xsl:when>
                        <xsl:when test="count(Name/ml)=0">
                            <xsl:value-of select="Name"/>
                        </xsl:when>
                    </xsl:choose>
                </xsl:param>

                <xsl:param name="URL"><xsl:value-of select="aspdnsf:EntityLink(EntityID, SEName, $EntityName, 0, '')" /></xsl:param>
                <td align="center">
                    <a href="{$URL}">
                        <xsl:value-of select="aspdnsf:LookupEntityImage(EntityID, $EntityName, 'icon', 0)" disable-output-escaping="yes" />
                    </a>
                    <br/>
                    <a href="{$URL}">
                        <xsl:value-of select="$scName" disable-output-escaping="yes"/>
                    </a>
                </td>
            </xsl:template>
        </xsl:stylesheet>
    </PackageTransform>

	<SearchEngineSettings>
		<SectionTitle actionType="transform">
            <xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:aspdnsf="urn:aspdnsf">
              <xsl:output method="html" omit-xml-declaration="yes" />
    	        <xsl:param name="LocaleSetting" select="/root/Runtime/LocaleSetting" />
	            <xsl:param name="WebConfigLocaleSetting" select="/root/Runtime/WebConfigLocaleSetting" />
	            <xsl:param name="EntityName"><xsl:value-of select="/root/QueryString/entity" /></xsl:param>
	            <xsl:param name="EntityACName">AppConfig.<xsl:value-of select="$EntityName"/>PromptPlural</xsl:param>
	            <xsl:param name="EntityDisplayName"><xsl:value-of select="aspdnsf:StringResource($EntityACName, $LocaleSetting)" /></xsl:param>
              <xsl:template match="/">
                All <xsl:value-of select="$EntityDisplayName" />
              </xsl:template>
            </xsl:stylesheet>
		</SectionTitle>
	</SearchEngineSettings>

</package>
